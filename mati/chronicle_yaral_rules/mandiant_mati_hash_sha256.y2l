rule mandiant_mati_hash_sha256 {

  meta:
    author = "thatsiemguy@"
    description = "Matches UDM Process Events against MATI Hash IOCs"
    severity = "LOW"
    priority = "LOW"
    known_limitations = "Detection Engine does not support Additionals in Outcomes at the time of publishing."

  events:
    (
        $e.metadata.event_type = "PROCESS_LAUNCH" or
        $e.metadata.event_type = "PROCESS_OPEN" or
        $e.metadata.event_type = "PROCESS_INJECTION" or
        $e.metadata.event_type = "PROCESS_MODULE_LOAD" or
        $e.metadata.event_type = "PROCESS_TERMINATION" or
        $e.metadata.event_type = "PROCESS_UNCATEGORIZED"                 
    )
    $e.principal.hostname = $asset and $e.principal.hostname != ""    
    $e.target.process.file.sha256 = $hash and $e.target.process.file.sha256 != ""    

    $g.graph.metadata.vendor_name = "MANDIANT_IOC" 
    $g.graph.metadata.entity_type = "FILE"
    $g.graph.entity.file.sha256 = $hash and $g.graph.entity.file.sha256 != ""

  match:
    $asset over 1m

  outcome:
    $risk_score = max(
        50 +
        if($e.security_result.action = "ALLOW", 10) -
        if($e.security_result.action = "BLOCK", 10)   
    )
    $oc_confidence_score = array_distinct($g.graph.metadata.threat.confidence_details)
    $oc_threat_id = array_distinct($g.graph.metadata.threat.threat_id)
    $oc_category_details = array_distinct($g.graph.metadata.threat.category_details)
    $oc_association_names = array_distinct($g.graph.metadata.threat.associations.name)

  condition:
    $e and $g
}
